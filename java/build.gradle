buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.1'
  }
}

ext {
    jdk = 1.8
    grpcVersion = '1.1.2'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'com.google.protobuf'
    apply plugin: 'maven-publish'

    group = 'spring-boot-grpc-example-proto'

    repositories {
        mavenCentral()
    }
    compileJava {
        sourceCompatibility = rootProject.jdk
        targetCompatibility = rootProject.jdk
        options.encoding = "UTF-8"
    }
    protobuf {
        protoc {
            artifact = 'com.google.protobuf:protoc:3.0.0'
        }
        plugins {
            grpc {
                artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
            }
        }
        generateProtoTasks {
            all()*.plugins {
                grpc {
                    option 'enable_deprecated=false'
                }
            }
        }
    }
    dependencies {
        compile "io.grpc:grpc-netty:${grpcVersion}"
        compile "io.grpc:grpc-protobuf:${grpcVersion}"
        compile "io.grpc:grpc-stub:${grpcVersion}"
    }
    publishing {
        repositories {
            maven {
                url 's3://repo.disc99.io/maven2'
                credentials(AwsCredentials) {
                    accessKey System.getenv('S3_MAVEN_ACCESS_KEY')
                    secretKey System.getenv('S3_MAVEN_SECRET_KEY')
                }
            }
        }
        
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
    }
}


project(":url-service") {
    version = '1.0'
    sourceSets.main.proto.srcDir '../../proto/UrlService'
}

project(":echo-service") {
    version = '2.0'
    sourceSets.main.proto.srcDir '../../proto/EchoService'
}